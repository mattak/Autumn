apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group            = 'me.mattak'
version          = '0.0.1'
archivesBaseName = 'autumn'

ext {
    archiveName  = 'autumn'
    userId       = 'mattak'
    userName     = 'Takuma Maruyama'
    userEmail    = 'mattak.me@gmail.com'
    gitUrl       = 'https://github.com/mattak/Autumn.git'
    siteUrl      = 'https://github.com/mattak/Autumn'
    issueUrl     = 'https://github.com/mattak/Autumn/issues'
    projectName  = 'Autumn'
    projectDescription = 'Android animator library inspired by Spring.'
    scmUrl       = 'scm:git@github.com:mattak/Autumn.git'
    licenseName  = 'The Apache License, Version 2.0'
    licenseUrl   = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    isReleaseVersion = !version.endsWith('SNAPSHOT')
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    jacoco {
        version = '0.7.2.201409121644'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'LICENSE'
    }
}

dependencies {
    compile "com.android.support:support-v4:23.1.1"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConvensions << {
    println project.getConvention()
}

if (gradle.startParameter.taskNames.contains('uploadArchives')) {
    apply from: 'publish_maven.gradle'
}